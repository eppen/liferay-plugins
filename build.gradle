apply from: "$gradle.lfrSdkDir/versions.gradle"

allprojects {
	apply plugin: "war"

	ant.loadproperties(srcFile: "$gradle.lfrSdkDir/build.properties")

	File pluginSrcDir = file("$projectDir/docroot")

	if (!pluginSrcDir.exists()) {
		pluginSrcDir = file("$projectDir/src")

		if (!pluginSrcDir.exists()) {
			pluginSrcDir = null
		}
	}

	if (pluginSrcDir == null) {
		return
	}

	task initGradle {
		def getBuildGradleDependencies
		def getBuildGradleDependenciesCompile
		def getBuildGradleDependenciesProvidedCompile
		def writeBuildGradleFile

		getBuildGradleDependencies = {
			Node ivyXmlNode, Properties pluginPackageProperties ->

			List contents = []

			contents << "dependencies {"

			contents.addAll(getBuildGradleDependenciesCompile(ivyXmlNode))
			contents.addAll(getBuildGradleDependenciesProvidedCompile(pluginPackageProperties))

			contents << "}"

			return contents
		}

		getBuildGradleDependenciesCompile = {
			Node ivyXmlNode ->

			List contents = []

			contents << "\tcompile("

			if (ivyXmlNode) {
				ivyXmlNode.dependencies.dependency.each {
					contents << "\t\t[group: \"${it.@org}\", name:\"${it.@name}\", version: \"${it.@rev}\"],"
				}
			}

			contents << "\t)"

			return contents
		}

		getBuildGradleDependenciesProvidedCompile = {
			Properties pluginPackageProperties ->

			List contents = []

			contents << "\tprovidedCompile("

			String portalDependencyJars = pluginPackageProperties["portal-dependency-jars"]

			if (portalDependencyJars != null) {
				portalDependencyJars = portalDependencyJars.replaceAll("\\.jar", "")
				portalDependencyJars = portalDependencyJars.replaceAll("-\\w") {
					it[1].toUpperCase()
				}

				String[] portalDependencyJarsArray = portalDependencyJars.split(",");

				portalDependencyJarsArray.each {
					portalDependencyJar ->

					Map portalDependencyJarsMap = portalDependencies.get(portalDependencyJar)

					if (portalDependencyJarsMap == null) {
						println("Portal dependency " + portalDependencyJar + " is not defined in versions.gradle")
					}
					else {
						contents << "\t\t[group: \"${portalDependencyJarsMap."group"}\", name: \"${portalDependencyJarsMap."name"}\", version: \"${portalDependencyJarsMap."version"}\"],"
					}
				}
			}

			contents << "\t)"

			return contents
		}

		writeBuildGradleFile = {
			Node ivyXmlNode, Properties pluginPackageProperties ->

			List contents = []

			contents.addAll(getBuildGradleDependencies(ivyXmlNode, pluginPackageProperties))

			File buildGradleFile = new File("$projectDir/build.gradle")

			contents.each {
				buildGradleFile << it + "\n"
			}
		}

		// build.xml

		XmlParser xmlParser = new XmlParser()

		xmlParser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)

		File buildXmlFile = new File("$projectDir/build.xml")

		Node buildXmlNode = xmlParser.parse(buildXmlFile)

		def getBuildXmlProperty = {
			key, defaultValue ->

			buildXmlData.property.each {
				if (it.@name == propName) {
					return it.@value
				}
			}

			return defaultValue
		}

		// ivy.xml

		Node ivyXmlNode = null

		File ivyXmlFile = new File("$projectDir/ivy.xml")

		if (ivyXmlFile.exists()) {
			ivyXmlNode = xmlParser.parse(ivyXmlFile)
		}

		// liferay-plugin-package.properties

		Properties pluginPackageProperties = new Properties()

		File pluginPackagePropertiesFile = new File("$projectDir/docroot/WEB-INF/liferay-plugin-package.properties")

		if (pluginPackagePropertiesFile.exists()) {
			pluginPackageProperties.load(new FileInputStream(pluginPackagePropertiesFile))
		}

		// build.gradle

		writeBuildGradleFile(ivyXmlNode, pluginPackageProperties)
	}
}